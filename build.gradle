// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.10"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'java'

repositories {
    maven {
        name "IGW"
        url "http://maven.k-4u.nl/"
    }
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // the repo from which to get NEI and stuff
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven { // the repo from which to get EnderIO and tterrags mods
        name 'tterrag'
        url "http://maven.tterrag.com/"
    }
    maven {
        name "OC repo"
        url "http://maven.cil.li/"
    }
    maven {
        name "ic2"
        url "http://maven.ic2.player.to/"
    }
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://tehnut.info/maven"
    }
    maven {
        url "http://maven.ironhide.de/"
    }
    maven {
        url 'http://maven.mcmoddev.com'
    }
    maven { // CoFH
        name = "Covers Maven"
        url = "http://maven.covers1624.net"
    }
    maven { url = "http://dl.tsr.me/artifactory/libs-release-local" }
    ivy {
        url "http://ae-mod.info/builds"
        layout "pattern", {
            artifact "[module]-[revision](-[classifier])(.[ext])"
        }
    }
}

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

version = "${minecraft_version}-${mod_version}"
if (hasProperty('appendBuildNumber')) {
    version = version + "-" + appendBuildNumber.toString()
} else {
    version = version + "-0"
}

group = "${group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_id}"

minecraft {
    version = "${forge_version}"
    runDir = "run"

    replace "@VERSION@", project.version
    replace "omlib@[0.0.0,)", "omlib@[" + omlib_version + ",)"
    replace "openmodularturrets@[0.0.0,)", "openmodularturrets@[" + omt_version + ",)"
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171212"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    //useDepAts = true
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def omlib_local = false
configure(allprojects.findAll { it.name == 'OMLib' }) {
    omlib_local = true
}

def omt_local = false
configure(allprojects.findAll { it.name == 'OpenModularTurrets' }) {
    omt_local = true
}

def ompd_local = false
configure(allprojects.findAll { it.name == 'OpenModularPassiveDefense' }) {
    ompd_local = true
}

def oml_local = false
configure(allprojects.findAll { it.name == 'OpenModularLighting' }) {
    oml_local = true
}

dependencies {
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}_${minecraft_version_short}"
    deobfCompile "li.cil.oc:OpenComputers:MC${minecraft_version}-${oc_version}"
    compileOnly "net.industrial-craft:industrialcraft-2:${ic2_version}:dev"
    compileOnly "net.darkhax.tesla:Tesla-${minecraft_version}:${tesla_version}:deobf"
    compileOnly "cofh:RedstoneFlux:${minecraft_version_short}-${redstoneflux_version}:deobf"

    if (!omlib_local) {
        compile "omtteam:omlib:${minecraft_version}-${omlib_version}:dev"
    } else {
        compile project(':OMLib')
    }
    if (!omt_local) {
        compile "omtteam:openmodularturrets:${minecraft_version}-${omt_version}:dev"
    } else {
        compile project(':OpenModularTurrets')
    }
    if (ompd_local) {
        runtime project(':OpenModularPassiveDefense')
    }
    if (oml_local) {
        runtime project(':OpenModularLighting')
    }
    //runtimeOnly "com.enderio:EnderIO:${minecraft_version}-${enderio_version}"
    //runtimeOnly "com.enderio.core:EnderCore:${minecraft_version}-${endercore_version}"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

tasks.build.dependsOn deobfJar, sourceJar

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            groupId group_prefix
            artifactId mod_id
            version minecraft_version + "-" + project.version
            artifact jar
            artifact sourceJar
            artifact deobfJar
        }
    }
    repositories {
        mavenLocal()
    }
}

//// DETECTING COMMIT NAME AND ACTIVATING CURSE RELEASE \\\\

def commit_release_type = getCommmitReleaseType()

def getCommmitReleaseType() {
    if (project.hasProperty("curseforge_key")) {
        def stdout = new ByteArrayOutputStream()
        try {
            exec {
                commandLine 'git', 'log', '-1', '--pretty=tformat:%s'
                standardOutput = stdout
            }
        } catch (Exception e) {
            System.print("SKIPPING CURSE RELEASE - FAILED TO RETRIEVE LAST COMMIT")
            e.printStackTrace()
            return 'none'
        }
        String commit = stdout.toString().trim()

        if (commit.startsWith("CRelease") || commit.startsWith("crelease")) {
            System.out.println("FOUND NEW RELEASE - COMMIT: ".concat(commit)); return 'release'
        } else if (commit.startsWith("CBeta") || commit.startsWith("cbeta")) {
            System.out.println("FOUND NEW BETA - COMMIT: ".concat(commit)); return 'beta'
        } else if (commit.startsWith("CAlpha") || commit.startsWith("calpha")) {
            System.out.println("FOUND NEW ALPHA - COMMIT: ".concat(commit)); return 'alpha'
        } else {
            System.out.println("SKIPPING CURSE RELEASE - COMMIT: " + commit); return 'none'
        }
    }
    System.out.println("SKIPPING CURSE RELEASE - NO CURSE API KEY")
    return 'none'
}

tasks.curseforge.enabled = project.hasProperty("curseforge_key") && !commit_release_type.equals('none')
curseforge {
    if (project.hasProperty("curseforge_key") && !commit_release_type.equals('none')) {
        System.out.println("ATTEMPTING CURSE RELEASE")
        apiKey = project.properties["curseforge_key"]
    } else {
        return
    }
    project {
        id = curse_project_id
        addGameVersion '1.12.2'

        relations {
            requiredDependency 'omlib'
            requiredDependency 'openmodularturrets'
            optionalDependency 'ompd'
            optionalDependency 'ender-io'
            optionalDependency 'redstone-flux'
            optionalDependency 'thermal-expansion'
        }

        mainArtifact(jar) {
            releaseType = commit_release_type
            changelog = file("${projectDir}/change_log.html").text
            changelogType = 'html'
        }

        addArtifact(sourceJar) {
            changelog = file("${projectDir}/change_log.html").text
            releaseType = 'alpha'
        }

        addArtifact(deobfJar) {
            changelog = file("${projectDir}/change_log.html").text
            releaseType = 'alpha'
        }
    }
}
